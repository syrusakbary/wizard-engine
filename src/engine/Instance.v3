// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Superclass of all import/exportable objects.
class Exportable {
	def render(buf: StringBuilder) -> StringBuilder {
		return buf.puts("<exportable>");
	}
}
// Abstract base class of host and wasm functions.
class Function(sig: SigDecl) extends Object {
}

// An instantiated wasm function, i.e. one bound to an instance.
class WasmFunction(instance: Instance, decl: FuncDecl) extends Function {
	new() super(decl.sig) { }
	def render(buf: StringBuilder) -> StringBuilder {
		return buf.put1("<wasm func %q>", renderShort);
	}
	def renderShort(buf: StringBuilder) -> StringBuilder {
		var names = if(instance != null, instance.module.names);
		return decl.render(names, buf);
	}
}
// An instantiated global.
class Global(valtype: ValueType, decl: GlobalDecl) extends Exportable {
	var value: Value;
}
// Superclass of all exported types.
class ExportedType extends Exportable {
}
// Wraps a source or host external type with its Wasm-level representation.
class ExportedValueType(src: ExportedType, vt: ValueType) extends ExportedType {
	def render(buf: StringBuilder) -> StringBuilder {
		if (src != null) buf.put2("<exported_type %q, %q>", src.render, vt.render);
		else buf.put1("<exported_type %q>", vt.render);
		return buf;
	}
}

// An instantiated module, including memories, tables, functions, etc.
class Instance(module: Module, imports: Array<Exportable>) {
	def memories = Array<Memory>.new(module.memories.length);
	def functions = Array<Function>.new(module.functions.length);
	def sigs = Array<SigDecl>.new(module.heaptypes.length);
	def sig_ids = Array<int>.new(module.heaptypes.length);
	var globals = Array<Global>.new(module.globals.length);
	def tables = Array<Table>.new(module.tables.length);
	def exports = Array<Exportable>.new(module.exports.length);
	def heaptypes = module.heaptypes; // TODO: instantiate heap types too
	def rtts = Array<RttObject>.new(module.heaptypes.length);
	def abstypes = Array<ExportedType>.new(module.abstypes.length);
	def defvals = Array<Value>.new(module.abstypes.length);
	def dropped_elems = Array<bool>.new(module.elems.length);
	def dropped_data = Array<bool>.new(module.data.length);

	def getFunctionAsVal(func_index: int) -> Value.Ref {
		return Value.Ref(functions[func_index]);
	}
	def evalInitExpr(init: InitExpr) -> Value {
		match (init) {
			I32(val) => return Value.I32(u32.view(val));
			I64(val) => return Value.I64(u64.view(val));
			F32(val) => return Value.F32(val);
			F64(val) => return Value.F64(val);
			V128(low, high) => return Value.V128(low, high);
			FuncRefNull => return Values.FUNCREF_NULL;
			ExternRefNull => return Values.REF_NULL;
			Global(global_index, g) => {
				return globals[global_index].value;
			}
			FuncRef(func_index, f) => {
				return Value.Ref(functions[func_index]);
			}
			Const(val) => return val;
			I31(val) => {
				var v = evalInitExpr(val);
				return Value.I31(u31.view(Values.v_i(v)));
			}
			Array(t, len, elem, rtt) => {
				var vrtt = RttObject.!(Value.Ref.!(evalInitExpr(rtt)).val);
				var vlen = Values.v_i(evalInitExpr(len));
				// TODO: initialize elements correctly
				return Value.Ref(HeapArray.new(vrtt, Array.new(vlen)));
			}
			Struct(t, vals, rtt) => {
				var vvals = Arrays.map(vals, evalInitExpr);
				var vrtt = RttObject.!(Value.Ref.!(evalInitExpr(rtt)).val);
				return Value.Ref(HeapStruct.new(vrtt, vvals));
			}
		}
	}
}
