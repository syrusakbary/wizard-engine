// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// WebAssembly program values.
type Value {
	case Ref(val: Object);
	case I31(val: u31);
	case I32(val: u32);
	case I64(val: u64);
	case F32(bits: u32);
	case F64(bits: u64);
	case V128(low: u64, high: u64);

	def render(buf: StringBuilder) -> StringBuilder { return Values.render(this, buf); }
}

// Superclass of all objects referred to by Value.Ref, including external refs.
class Object extends Exportable { }

// Objects allocated on the "wasm" GC heap, i.e. from the GC proposal.
class HeapObject(rtt: RttObject, vals: Array<Value>) extends Object {}
class HeapStruct extends HeapObject {
	new(rtt: RttObject, vals: Array<Value>) super(rtt, vals) { }
}
class HeapArray extends HeapObject {
	new(rtt: RttObject, vals: Array<Value>) super(rtt, vals) { } // XXX: unboxed prim arrays
}

// Runtime type identifiers.
class RttObject(decl: HeapTypeDecl, supers: Array<RttObject>) extends Object { }

// Utilities associated with values.
component Values {
	def I32_1 = Value.I32(1);
	def I32_0 = Value.I32(0);
	def I64_0 = Value.I64(0);
	def F32_0 = Value.F32(0);
	def F64_0 = Value.F64(0);
	def F32_minus_0		= Value.F32(0x8000_0000);
	def F64_minus_0		= Value.F64(0x8000_0000_0000_0000);
	def F32_nan		= Value.F32(0x7fc0_0000);
	def F64_nan		= Value.F64(0x7ff8_0000_0000_0000);
	def F32_infinity	= Value.F32(0x7f80_0000);
	def F64_infinity	= Value.F64(0x7ff0_0000_0000_0000);
	def F32_minus_infinity	= Value.F32(0xff80_0000);
	def F64_minus_infinity	= Value.F64(0xfff0_0000_0000_0000);
	def FUNCREF_NULL = Value.Ref(null);
	def REF_NULL = FUNCREF_NULL;
	def NONE = Array<Value>.new(0);
	def NO_SUPERS = Array<RttObject>.new(0);
	def RTT_ANYREF = RttObject.new(null, NO_SUPERS);

	def render(v: Value, buf: StringBuilder) -> StringBuilder {
		match (v) {
			Ref(val) => match (val) {
				x: RttObject => {
					if (x.decl != null) {
						buf.put2("<rtt %d %q>", if(x.supers != null, x.supers.length, -1), x.decl.render);
					} else {
						buf.puts("<rtt any>");
					}
				}
				x: HostObject => buf.put1("<externref %q>", x.render);
				x: WasmFunction => buf.put1("<funcref: #%d>", x.decl.func_index);
				x: HostFunction => x.render(buf);
				x: HeapStruct => {
					var id = if(x.rtt == null, -1, StructDecl.!(x.rtt.decl).heaptype_index);
					buf.put1("<ref struct #%d>", id);
				}
				x: HeapArray => {
					var id = if(x.rtt == null, -1, ArrayDecl.!(x.rtt.decl).heaptype_index);
					buf.put1("<ref array #%d>", id);
				}
				null => buf.puts("<ref null>");
			}
			I31(val) => buf.put1("i31:%d", u32.view(val));
			I32(val) => buf.put1("%d", val);
			I64(val) => buf.put1("%duL", val);
			F32(val) => buf.put1("f32:%x", val);
			F64(val) => buf.put1("f64:%x", val);
			V128(low, high) => buf.put2("v128:%x%x", high, low);
		}
		return buf;
	}
	def renderVals(buf: StringBuilder, av: Array<Value>) -> StringBuilder {
		buf.putc('(');
		if (av != null) for (i < av.length) {
			if (i > 0) buf.puts(", ");
			av[i].render(buf);
		}
		buf.putc(')');
		return buf;
	}
	def isNull(v: Value) -> bool {
		return v == REF_NULL;
	}
	def isFunc(v: Value) -> bool {
		return Value.Ref.?(v) && Function.?(Value.Ref.!(v).val);
	}
	def isData(v: Value) -> bool {
		match (v) {
			Ref(o) => return HeapObject.?(o) || HeapArray.?(o);
			_ => return false;
		}
	}
	def isArray(v: Value) -> bool {
		match (v) {
			Ref(o) => return HeapArray.?(o);
			_ => return false;
		}
	}
	def isI31(v: Value) -> bool {
		return Value.I31.?(v);
	}
	def default(t: ValueType) -> Value {
		var v: Value;
		match (t) {
			BOTTOM => v = REF_NULL; // TODO: no default for bottom
			I32 => v = I32_0;
			I64 => v = I64_0;
			F32 => v = F32_0;
			F64 => v = F64_0;
			V128 => v = I32_0;  // TODO: default value for SIMD
			Abstract, // TODO: default value for abstract type
			Host,
			Ref => v = REF_NULL;
		}
		return v;
	}
	def v_i(v: Value) -> i32 { return i32.view(Value.I32.!(v).val); }
	def v_u(v: Value) -> u32 { return Value.I32.!(v).val; }
	def v_f(v: Value) -> u32 { return Value.F32.!(v).bits; }
	def v_d(v: Value) -> u64 { return Value.F64.!(v).bits; }
	def v_l(v: Value) -> i64 { return i64.view(Value.I64.!(v).val); }
	def v_w(v: Value) -> u64 { return Value.I64.!(v).val; }

	def i_v(v: i32) -> Value { return Value.I32(u32.view(v)); }
	def u_v(v: u32) -> Value { return Value.I32(v); }
	def l_v(v: i64) -> Value { return Value.I64(u64.view(v)); }
	def w_v(v: u64) -> Value { return Value.I64(v); }

	def f32(sign: int, exp: int, mantissa: u32) -> Value.F32 {
		var bits = if(sign != 0, 0x80000000u);
		if (exp < -149) return Value.F32(bits);
		if (exp <= -127) {
			// subnormal; make implicit 1 explicit and shift right
			mantissa = 0x80000000u | mantissa >> 1;
			mantissa = mantissa >> u5.view(-127 - exp);
			exp = -127;
		} else if (exp >= 128) {
			// saturate large exponents to infinity
			return if (sign != 0, F32_minus_infinity, F32_infinity);
		}
		bits |= (u32.view(u8.view(exp + 127)) << 23);
		bits |= u32.view(mantissa >> 9);
		return Value.F32(bits);
	}
	def f64(sign: int, exp: int, mantissa: u64) -> Value.F64 {
		var bits = if(sign != 0, 0x8000000000000000ul);
		if (exp < -1076) return Value.F64(bits);
		if (exp <= -1023) {
			// subnormal; make implicit 1 explicit and shift right
			mantissa = 0x8000000000000000ul | mantissa >> 1;
			mantissa = mantissa >> u6.view(-1023 - exp);
			exp = -1023;
		} else if (exp >= 1024) {
			// saturate large exponents to infinity
			return if (sign != 0, F64_minus_infinity, F64_infinity);
		}
		bits |= (u64.view(u11.view(exp + 1023)) << 52);
		bits |= mantissa >> 12;
		return Value.F64(bits);
	}
	def f32nan(sign: int, mantissa: u32) -> Value.F32 {
		var bits = if(sign != 0, 0x80000000u);
		bits |= (u32.view(u8.view(255)) << 23);
		bits |= u32.view(mantissa >> 9);
		return Value.F32(bits);
	}
	def f64nan(sign: int, mantissa: u64) -> Value.F64 {
		var bits = if(sign != 0, 0x8000000000000000ul);
		bits |= (u64.view(u11.view(2047)) << 52);
		bits |= mantissa >> 12;
		return Value.F64(bits);
	}
	def subRtt(rtt: RttObject, decl: HeapTypeDecl) -> RttObject {
		var sup = rtt.supers;
		var sub = Arrays.grow(sup, sup.length + 1);
		var r = RttObject.new(decl, sub);
		sub[sup.length] = r;
		return r;
	}
	def newRtt(decl: HeapTypeDecl) -> RttObject {
		return RttObject.new(decl, NO_SUPERS);
	}
	def toFuncAndId(val: Value) -> (Function, int) {
		match (val) {
			Ref(val) => match (val) {
				x: Function => return (x, Canon.sigId(x.sig));
			}
			_ => ;
		}
		return (null, -1);
	}
}
// Configures the representation of values on a (native) value stack.
class ValueRep(tagged: bool, simd: bool) {
	def raw = if(simd, 16, 8);
	def tag_size: int = if(tagged, raw, 0);
	def slot_size: int = if(tagged, raw * 2, raw);
	def slot_size_log: u6 = if(tagged, if(simd, 5, 4), if(simd, 4, 3));
}
